#!/usr/bin/perl
use strict;
use warnings;
use Coro;
use Coro::Channel;
use Coro::Semaphore;
use Coro::LWP;
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON::XS;
use Term::ANSIColor;

binmode STDOUT, ":utf8";

my $ua = LWP::UserAgent->new;
$ua->default_header('X-Remedie-Client' => 'CLI');

my $api_call = sub {
    my($method, $path, $args) = @_;
    my $uri = URI->new("http://localhost:10010/rpc$path");

    my $res;
    if ($method eq 'get' && $args) {
        $uri->query_form(%$args);
        $res = $ua->get($uri);
    } else {
        $res = $ua->$method($uri, $args);
    }

    return JSON::XS->new->decode($res->content);
};

my $workers = 5;

my @channels = @ARGV ? get_channels(@ARGV) : all_channels();
my $queue = Coro::Channel->new;

# provider thread
async {
    for my $channel (@channels) {
        $queue->put($channel);
    }
};

# worker threads
my @workers;
for my $i (1..$workers) {
    async {
        my $lock = Coro::Semaphore->new(0);
        push @workers, $lock;
        while ($queue->size > 0) {
            my $channel = $queue->get;
            my $data = $api_call->("post", "/channel/refresh", { id => $channel->{id} });
            my $new = $data->{channel}{unwatched_count} - $channel->{unwatched_count};
            printf "%d: %s updated %s\n",
              $channel->{id}, $data->{channel}{name}, ($new ? colored(['red'], "($new)") : "");
        }
        $lock->up;
    };
}

async {
    $_->down for @workers;
    exit;
};

schedule;

sub get_channels {
    my @ids = @_;
    map $api_call->("get", "/channel/show", { id => $_ })->{channel}, @ids;
}

sub all_channels {
    @{ $api_call->("get", "/channel/load")->{channels} };
}
