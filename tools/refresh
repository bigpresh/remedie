#!/usr/bin/perl
use strict;
use warnings;
use Coro;
use Coro::Semaphore;
use Coro::LWP;
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON::XS;
use Term::ANSIColor;

binmode STDOUT, ":utf8";

my $ua = LWP::UserAgent->new;
$ua->default_header('X-Remedie-Client' => 'CLI');

my $api_call = sub {
    my($method, $path, $args) = @_;
    my $uri = URI->new("http://localhost:10010/rpc$path");

    my $res;
    if ($method eq 'get' && $args) {
        $uri->query_form(%$args);
        $res = $ua->get($uri);
    } else {
        $res = $ua->$method($uri, $args);
    }

    return JSON::XS->new->decode($res->content);
};

my $workers = 20;

my @channels = @ARGV ? get_channels(@ARGV) : all_channels();
my $lock = Coro::Semaphore->new($workers);

my @coros;
for my $channel (@channels) {
    push @coros, async {
        my $guard = $lock->guard;
        my $data = $api_call->("post", "/channel/refresh", { id => $channel->{id} });
        my $new = $data->{channel}{unwatched_count} - $channel->{unwatched_count};
        printf "%d: %s updated %s\n",
            $channel->{id}, $data->{channel}{name}, ($new ? colored(['red'], "($new)") : "");
    };
}

$_->join for @coros;

sub get_channels {
    my @ids = @_;
    map $api_call->("get", "/channel/show", { id => $_ })->{channel}, @ids;
}

sub all_channels {
    @{ $api_call->("get", "/channel/load")->{channels} };
}
