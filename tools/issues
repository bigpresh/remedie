#!/usr/bin/perl
use strict;
use warnings;
use YAML::XS;
use Net::GitHub;

my %github = (owner => 'miyagawa', repo => 'remedie');
chomp($github{login} = `git config github.user`);
chomp($github{token} = `git config github.token`);

my $github = Net::GitHub->new(%github);

my $cmd = shift || "list";
my %cmds = (
    list => \&run_list,
    get  => \&run_details,
    view => \&run_details,
);

my $command = $cmds{$cmd} or die "Command $cmd not found\n";
$command->($github, @ARGV);

sub run_list {
    my($github, $search) = @_;

    my $issues = $search
        ? $github->issue->search('open', $search) : $github->issue->list('open');
    for my $issue (@$issues) {
        printf "%s: %s\n",
            $issue->{number}, $issue->{title};
    }
}

sub run_details {
    my($github, $id) = @_;

    my $issue = $github->issue->view($id);
    print Dump $issue;
}
