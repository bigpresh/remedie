package Remedie::Server::RPC::Collection;
use Moose;

BEGIN { extends 'Remedie::Server::RPC' }

__PACKAGE__->meta->make_immutable;

no Moose;


use Remedie;
use Remedie::DB::Channel;
use DateTime;
use DateTime::Format::Mail;
use Encode;
use Template;

sub opml {
    my($self, $req, $res) = @_;

    my $channels = Remedie::DB::Channel::Manager->get_channels();

    my $stash = {
        channels => $channels,
        now => DateTime::Format::Mail->format_datetime( DateTime->now ),
        version => Remedie->VERSION,
        encode_xml => sub {
            local $_ = shift;
            s/&/&#38;/g;
            s/</&lt;/g;
            s/>/&gt;/g;
            s/"/&#34;/g;
            s/'/&#39;/g;
            $_;
        },
    };

    my $tt = Template->new;
    $tt->process(\<<TEMPLATE, $stash, \my $out) or die $tt->error;
<?xml version="1.0" encoding="utf-8"?>
<!-- OPML generated by Remedie [% version %] -->
<opml version="2.0">
<head>
<title>remedie_subscription.opml</title>
<dateCreated>[% now %]</dateCreated>
<docs>http://www.opml.org/spec2</docs>
</head>
<body>
<outline text="Subscriptions">
[% FOREACH channel = channels -%]
<outline type="rss" text="[% encode_xml(channel.name) %]"[% IF channel.props.link %] htmlUrl="[% encode_xml(channel.props.link) %]"[% END %] xmlUrl="[% encode_xml(channel.ident) %]" />
[% END -%]
</outline>
</body>
</opml>
TEMPLATE

    $res->status(200);
    $res->content_type("text/xml; charset=utf-8");
    $res->body( encode_utf8($out) );

    return { success => 1 };
}

1;
